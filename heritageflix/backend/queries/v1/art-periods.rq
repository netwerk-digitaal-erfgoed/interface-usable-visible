# Generate art periods and styles
# SPARQL endpoint: http://vocab.getty.edu/sparql
# UI of the SPARQL endpoint: http://vocab.getty.edu/sparql

PREFIX aat: <http://vocab.getty.edu/aat/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX gvp: <http://vocab.getty.edu/ontology#>
PREFIX schema: <https://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX skosxl: <http://www.w3.org/2008/05/skos-xl#>
PREFIX void: <http://rdfs.org/ns/void#>

CONSTRUCT {
  ?artPeriod a schema:DefinedTerm ;
    schema:name ?prefLabel ;
    schema:startDate ?startDate ;
    schema:endDate ?endDate .
}
WHERE {
  # For testing specific resources
  # VALUES ?artPeriod {
  #   aat:300056513
  # }

  ?artPeriod a skos:Concept ;
    void:inDataset <http://vocab.getty.edu/dataset/aat> ;
    skosxl:prefLabel ?prefLabel_uri .

  ?artPeriod gvp:broaderPreferredExtended ?broaderPreferredExtended .
  VALUES ?broaderPreferredExtended {
    aat:300264088 # "Styles and Periods Facet"
    aat:300163706 # "forms of expression (artistic concept)"
  }

  ?prefLabel_uri dcterms:language aat:300388256 ; # Dutch (language)
  skosxl:literalForm ?prefLabel .

  BIND(
    COALESCE(
      IF(?artPeriod = aat:300020756, 1000, ?noPeriodFound), # Medieval
      IF(?artPeriod = aat:300021140, 1450, ?noPeriodFound), # Renaissance
      IF(?artPeriod = aat:300021147, 1600, ?noPeriodFound), # Baroque
      IF(?artPeriod = aat:300056513, 1750, ?noPeriodFound), # Classicism
      IF(?artPeriod = aat:300172861, 1830, ?noPeriodFound), # Realist
      IF(?artPeriod = aat:300021503, 1860, ?noPeriodFound), # Impressionist
      IF(?artPeriod = aat:300021474, 1890, ?noPeriodFound), # Modernist
      IF(?artPeriod = aat:300022208, 1970, ?noPeriodFound) # Postmodern
    )
    AS ?startDate
  )

  BIND(
    COALESCE(
      IF(?artPeriod = aat:300020756, 1450, ?noPeriodFound), # Medieval
      IF(?artPeriod = aat:300021140, 1600, ?noPeriodFound), # Renaissance
      IF(?artPeriod = aat:300021147, 1750, ?noPeriodFound), # Baroque
      IF(?artPeriod = aat:300056513, 1850, ?noPeriodFound), # Classicism
      IF(?artPeriod = aat:300172861, 1870, ?noPeriodFound), # Realist
      IF(?artPeriod = aat:300021503, 1890, ?noPeriodFound), # Impressionist
      IF(?artPeriod = aat:300021474, 1970, ?noPeriodFound) # Modernist
    )
    AS ?endDate
  )
}
LIMIT 100000
